;*************************************************
; heatscatter.ncl
;
; Concepts illustrated:
;   - Drawing a scatter plot with markers of different colors
;
;*************************************************
;This function is to generate density scatter plot color map
undef("heatscatter")
function heatscatter(x[*]:numeric,y[*]:numeric,numbins:numeric,mapname:string)
local UNDEF,pdf2,onepercent,values,\
      centers_X,centers_Y,binsize_X,binsize_Y,\
      bins_X,bins_Y,i,scatter_COL,\
      last_lower_X,last_lower_Y,last_higher_X,last_higher_Y,\
      last_lower_X_inds, last_higher_X_inds,\
      last_lower_Y_inds, last_higher_Y_inds,\
      id_X,id_Y,c_X,c_Y,cmap,subcmap,szcmap,ncolor,colors
begin
 
  UNDEF  = -999
  pdf2       = pdfxy(x,y,numbins,numbins,False)
 ;print(pdf2)
  onepercent = round(dimsizes(x) / 100,0)
  print("onepercent= "+onepercent)
  
  ;values     = tofloat(pdf2*onepercent)
   values     = tofloat(pdf2*100)
  print("****** Min Max of Values *******")
  printMinMax(values, True)  
  print("********************************")
    
  centers_X = tofloat(pdf2@binx_center)
  centers_Y = tofloat(pdf2@biny_center)    

  binsize_X = abs(centers_X(1) - centers_X(0)) / 2
  binsize_Y = abs(centers_Y(1) - centers_Y(0)) / 2

  bins_X = new((/numbins, 2/),typeof(x))
  bins_Y = new((/numbins, 2/),typeof(y))
  bins_X = 0
  bins_Y = 0

  last_higher_X = 0
  last_higher_X@_FillValue = UNDEF
  last_lower_X  = 0
  last_lower_X@_FillValue  = UNDEF 
  
  last_higher_Y = 0
  last_higher_Y@_FillValue = UNDEF
  last_lower_Y  = 0
  last_lower_Y@_FillValue  = UNDEF 

  id_X = 0
  id_X@_FillValue  = UNDEF
  id_Y = 0
  id_Y@_FillValue  = UNDEF
  do i = 0,numbins-1
        bins_X(i, 0) = centers_X(i) - binsize_X
        bins_X(i, 1) = centers_X(i) + binsize_X
        bins_Y(i, 0) = centers_Y(i) - binsize_Y
        bins_Y(i, 1) = centers_Y(i) + binsize_Y
  end do;i
 ;print(bins_X+" *** "+bins_Y)

  print("Generating colormap indices......");
  scatter_COL = new(dimsizes(x), typeof(x));
  scatter_COL = 0
  do i = 0,dimsizes(x)-1
        if ((mod(i,onepercent) .eq. 0).and.(i.le.50)) then
            print("..........")
        end if
        
        last_higher_X = UNDEF
        last_lower_X  = UNDEF
        id_X          = UNDEF
        c_X = x(i)
        
        last_lower_X_inds := ind(c_X .ge. bins_X(:,0))
        last_lower_X_inds@_FillValue  = UNDEF
        if (.not.all(ismissing(last_lower_X_inds))) then
            nlast_lower_X = dimsizes(last_lower_X_inds )
            last_lower_X  = last_lower_X_inds(nlast_lower_X-1)            
        else
            last_higher_X_inds := ind(c_X .le. bins_X(:,1))
            last_higher_X_inds@_FillValue = UNDEF
            if (.not.all(ismissing(last_higher_X_inds))) then
                last_higher_X = last_higher_X_inds(0)               
            end if 
        end if
       
        if (.not.ismissing(last_lower_X)) then
            id_X = last_lower_X
        else
              if (.not.ismissing(last_higher_X)) then
                id_X = last_higher_X
            end if
        end if
         
        last_higher_Y = UNDEF
        last_lower_Y  = UNDEF
        id_Y          = UNDEF
        c_Y = y(i)
        last_lower_Y_inds := ind(c_Y .ge. bins_Y(:,0)) 
        last_lower_Y_inds@_FillValue = UNDEF
        if (.not.all(ismissing(last_lower_Y_inds))) then
            nlast_lower_Y=dimsizes(last_lower_Y_inds)
            last_lower_Y := last_lower_Y_inds(nlast_lower_Y-1)            
        else
            last_higher_Y_inds := ind(c_Y .le. bins_Y(:,1))
            last_higher_Y_inds@_FillValue = UNDEF
            if (.not.all(ismissing(last_higher_Y_inds))) then
                last_higher_Y := last_higher_Y(0)
            end if
        end if
        if (.not.ismissing(last_lower_Y)) then
            id_Y = last_lower_Y
        else
            if (.not.ismissing(last_higher_Y)) then
                id_Y = last_higher_Y
            end if
        end if 
        ;print(i+"---"+id_X+" Index "+id_Y)
        if (.not.ismissing(id_X)) .and.(.not.ismissing(id_Y)) then
            scatter_COL(i) = values(toint(id_X), toint(id_Y));
        end if
    end do;i
    print("***** Min Max value of color indicies *****")
    printMinMax(scatter_COL,True)
    print("*******************************************")


    print("***** get colors ******")
    cmap    = read_colormap_file(mapname)
    subcmap = cmap(2:,:)
    szcmap  = dimsizes(subcmap)
    ncolor  = szcmap(0)
    
    if ncolor.lt.toint(max(scatter_COL)) then
       print(" the color indicies exceed the maxmum of the color map")
       exit
    else 
       colors=subcmap(toint(scatter_COL),:)     
    end if
    return(colors)
end    
