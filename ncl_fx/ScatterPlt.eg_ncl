;***************************************** 
;	This script is for Scatter Plotting
;*****************************************
load "./statistics.ncl"
load "./heatscatter.ncl"
load "./attach_labelbar.ncl"
;********************************************************
undef("HeatScatterPlt")
function HeatScatterPlt(wks:graphic,\
                         x[*]:numeric,\
                         y[*]:numeric,\
                         limits[3]:numeric,\ 
                         nStd:numeric,\
                         optOutlier:logical,\
                         tiStrs[*]:string,\  
                         opt)
 ;xlim(0),xlim(1),bin_num(2)
 ;tiX;tiY;figId
local diffxy,Nstr,textstr,colormaps,ncolor,idat,inds,UNDEF,color_id,idxc,rc,z,res,respl,plot,txres,\
xp,yp,ntym,ntxm,dum1,dum6,labels,labelbar

begin
    print("...........heatscatter plot...........")
    UNDEF  = x@_FillValue
    diffxy = xyStatistics(x,y,nStd,optOutlier,0)
    printVarSummary(diffxy)
    x = where(ismissing(diffxy),UNDEF,x)
	y = where(ismissing(diffxy),UNDEF,y)
 
    if optOutlier then
        Nstr =  "N: "+diffxy@N;+" ("+sprintf("%.1f",tofloat(diffxy@N)/tofloat(diffxy@dof)*100.)+"%)"
    else
         Nstr =  "N: "+diffxy@dof
    end if;optOutlier
	;textstr = (/"Corr: "+sprintf("%.2f",diffxy@Corr),\
	;		  "Bias: "+sprintf("%.2f",diffxy@Bias),\
	;		  "RMSE: "+sprintf("%.2f",diffxy@RMS),\
	;		  "Std: "+sprintf("%.2f",diffxy@Std)/)  
    
    textstr = "Corr: "+sprintf("%.2f",diffxy@Corr)+"~C~"+\
			  "Bias: "+sprintf("%.2f",diffxy@Bias)+"~C~"+\
			  "RMSE: "+sprintf("%.2f",diffxy@RMS)+"~C~"+\
			  "Std: "+sprintf("%.2f",diffxy@Std)+"~C~"+Nstr
    print(textstr)
		  
  
	;wksname   = wksname+"_nbin"+bin_num
	colormaps = read_colormap_file(opt@cmap)
	ncolor   = dimsizes(colormaps(:,0))

	idat = ind(.not.ismissing(diffxy))
	if .not.all(ismissing(idat)) then
	    x:= x(idat)
	    y:= y(idat) 
	    x@_FillValue =UNDEF
        y@_FillValue =UNDEF
	end if
	delete(idat)
	 
	 
	color_id = heatscatter(x,y,limits(2),limits(0:1),False) 	 
	printMinMax(color_id@scatter_num,True)
	idxc   = toint((color_id@scatter_num-min(color_id@scatter_num))/(max(color_id@scatter_num)-min(color_id@scatter_num))*(ncolor-1))
    printMinMax(idxc,True)
    ;+++ Best fit
	rc		= regline(x,y)
    z       = rc*(x-rc@xave)+rc@yave
 
;----- open workstation			   
   ;wks   = gsn_open_wks (wkstype,pltpth+"heatscatter_"+wksname)            
    res                    = True
    res@gsnDraw            = False
    res@gsnFrame           = False
    ;res@gsnMaximize       = True

    res@vpXF                  = 0.2
    res@vpYF                  = 0.85
    res@vpHeightF             = 0.575;0.6
    res@vpWidthF              = 0.575;0.6

    res@tiMainString      = ""
    res@tiMainFontHeightF = 0.02
    res@tiMainOffsetYF    = 0.05

    res@trXMinF = limits(0)
    res@trXMaxF = limits(1)
    res@trYMinF = limits(0)
    res@trYMaxF = limits(1)

    res@tmXBMajorThicknessF     = 4
    res@tmYLMajorThicknessF     = 4
    res@tmXBLabelFontThicknessF = 4
    res@tmYLLabelFontThicknessF = 4
	
   ;res@tmXBMode              = "Manual"
   ;res@tmXBTickStartF        = xlim(0)
   ;res@tmXBTickEndF          = xlim(1)
   ;res@tmXBTickSpacingF      = 50
   ;res@tmXBLabelStride= 1

     res@tmBorderThicknessF= 4.0
     
     res@tmLabelAutoStride = True
     res@tmYROn            = False
     res@tmXTOn            = False
     res@tmYLMinorOn       = False
     res@tmXBMinorOn       = False
     res@tmXBLabelFontHeightF    = 0.03
     res@tmYLLabelFontHeightF    = 0.03

     res@tmYLLabelDeltaF   = -0.65
     res@tmXBLabelDeltaF   = -0.55

     res@tiYAxisString         = tiStrs(1)
     res@tiYAxisOffsetXF       = 0.01
     res@tiYAxisFontThicknessF = 5
     res@tiYAxisFontHeightF    = 0.03

     res@tiXAxisString         = tiStrs(0)
    ;res@tiXAxisOffsetYF       = 0.01
     res@tiXAxisFontThicknessF = 5
     res@tiXAxisFontHeightF    = 0.03
;---Set some marker resources

    if opt@style.ne."heat" then
        res@xyMarkLineModes   = "Markers"                
        res@xyMarkers         =  NhlNewMarker(wks,"u",34,0.0,0.0,1.3125,1.5,0.0); filled triangle;16;
        res@xyMarkerColor     = "blue"                    
        res@xyMarkerSizeF     = 0.01                     ; default 0.01
        plot = gsn_csm_xy (wks,x,y,res)
    else
        res@xyMarkLineMode     = "Markers"
        res@xyMarker           = 16
        ;res@xyMarkerThicknessF = 2.5
        res@xyMarkerSizeF      = 0.05/20.
        res@xyMonoMarkerColor = False
        res@xyMarkerColors     = colormaps(idxc,:)
   
        plot = gsn_csm_xy (wks,transpose((/x,x/)), \
                         transpose((/y,y/)),res)
    
    end if ; opt.ne."heat" 
    
  
    respl   = True
    respl@gsLineDashPattern     = 16
    respl@gsLineColor       = "black"
    respl@gsLineThicknessF  = 3.
    
    plot@$unique_string("plotpl")$ = gsn_add_polyline(wks, plot, limits(0:1),limits(0:1), respl)
       
  
    getvalues plot
        "tmXBValues"             : tmXBValues
        "tmYLValues"             : tmYLValues
        "tmYLLabelFontHeightF"   : fheight
        "tmYLLabelFontThicknessF": fthick
    end getvalues
    setvalues plot                    
        "tmYLLabelFontHeightF"   : fheight*1.075
		"tmXBLabelFontHeightF"   : fheight*1.075
		"tmYLLabelFontThicknessF": fthick*1.3*2
		"tmXBLabelFontThicknessF": fthick*1.3*2
		"tiXAxisFontHeightF"     : fheight*1.2
		"tiXAxisFontThicknessF"  : fthick*1.3*2
		"tiYAxisFontHeightF"     : fheight*1.2
		"tiYAxisFontThicknessF"  : fthick*1.3*2

    end setvalues
	txres               = True
	txres@txFontHeightF = fheight*0.85
    txres@txFontThicknessF = fthick*1.3	
    ntym = dimsizes(tmYLValues)
    ntxm = dimsizes(tmXBValues)
    txxp = abs(abs(tmXBValues(1))-abs(tmXBValues(0)))
    txyp = abs(abs(tmYLValues(1))-abs(tmYLValues(0)))
     if opt then
        txres@txJust        = "TopLeft"
        xp = limits(0)+txxp*0.25
        yp = limits(1)
        dum1=gsn_add_text(wks,plot,textstr,xp,yp-0.25*txyp,txres)

        ;dum1=gsn_add_text(wks,plot,textstr(0),xp,yp-0.25*txyp,txres)
        ;dum2=gsn_add_text(wks,plot,textstr(1),xp,yp-0.55*txyp,txres)
        ;dum3=gsn_add_text(wks,plot,textstr(2),xp,yp-0.85*txyp,txres)
        ;dum4=gsn_add_text(wks,plot,textstr(3),xp,yp-1.15*txyp,txres)
        ;dum5=gsn_add_text(wks,plot,Nstr      ,xp,yp-1.45*txyp,txres)
    end if
    txres@txJust        = "BottomRight"
    txres@txFontColor   = "red"
         
    xp  := limits(1)-txxp*0.25
    yp  := limits(0)+txxp*0.25
  
    dum6=gsn_add_text(wks,plot,tiStrs(2) ,xp,yp,txres)
 
     ;res@xyMarkLineModes   = "Lines"
     ;res@xyLineThicknesses = 5.
     ;res@xyLineColor       = "red"
    ; plotl2  = gsn_csm_xy (wks,x,z,res)
     ;overlay(plot,plotl2)
    
	if opt@style.eq."heat" then
	    labels   = toint(fspan(0,max(color_id@scatter_num),ncolor))
		labels  := tostring(labels)
        labels(0)=""

        labelbar = attach_labelbar(wks,plot,labels,colormaps)
     end if
    ;printVarSummary(plot)
     return(plot)
end 